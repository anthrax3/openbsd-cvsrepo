head	1.6;
access;
symbols
	OPENBSD_6_2_BASE:1.6
	OPENBSD_6_1:1.6.0.4
	OPENBSD_6_1_BASE:1.6
	OPENBSD_6_0:1.4.0.4
	OPENBSD_6_0_BASE:1.4
	OPENBSD_5_9:1.4.0.2
	OPENBSD_5_9_BASE:1.4
	OPENBSD_5_8:1.3.0.8
	OPENBSD_5_8_BASE:1.3
	OPENBSD_5_7:1.3.0.2
	OPENBSD_5_7_BASE:1.3
	OPENBSD_5_6:1.3.0.4
	OPENBSD_5_6_BASE:1.3
	OPENBSD_5_5:1.2.0.8
	OPENBSD_5_5_BASE:1.2
	OPENBSD_5_4:1.2.0.4
	OPENBSD_5_4_BASE:1.2
	OPENBSD_5_3:1.2.0.2
	OPENBSD_5_3_BASE:1.2;
locks; strict;
comment	@# @;


1.6
date	2016.12.22.15.33.36;	author visa;	state Exp;
branches;
next	1.5;
commitid	sw48d58czeutOmw2;

1.5
date	2016.09.06.13.41.29;	author visa;	state Exp;
branches;
next	1.4;
commitid	YLWh6Z5nXoTUOOVU;

1.4
date	2015.09.20.11.52.33;	author miod;	state Exp;
branches;
next	1.3;
commitid	fs2tQxw4pLEx70Sd;

1.3
date	2014.04.09.21.10.35;	author miod;	state Exp;
branches;
next	1.2;

1.2
date	2012.10.03.11.18.23;	author miod;	state Exp;
branches;
next	1.1;

1.1
date	2012.09.29.21.37.03;	author miod;	state Exp;
branches;
next	;


desc
@@


1.6
log
@Extend the size of user virtual address space from 2GB to 1TB on mips64
by adding another level to page directories. This improves ASLR and
complements W^X added earlier on some systems, giving a notable update
to the architecture's security. Besides, there is now more room for
running tasks that hog memory.

Testing help from deraadt@@ and fcambus@@.
Platforms tested: loongson, octeon, sgi/IP27 and sgi/IP30
(IP30 also with 4KB pages).
@
text
@/*	$OpenBSD: exception_tfp.S,v 1.5 2016/09/06 13:41:29 visa Exp $	*/

/*
 * Copyright (c) 2012 Miodrag Vallat.
 *
 * Permission to use, copy, modify, and distribute this software for any
 * purpose with or without fee is hereby granted, provided that the above
 * copyright notice and this permission notice appear in all copies.
 *
 * THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
 * WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
 * MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
 * ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
 * WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
 * ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
 * OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
 */
/*
 * k_general and u_general are heavily based upon their counterparts in
 * exception.S under the following licence terms:
 */
/*
 * Copyright (c) 2002-2003 Opsycon AB  (www.opsycon.se / www.opsycon.com)
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 * 1. Redistributions of source code must retain the above copyright
 *    notice, this list of conditions and the following disclaimer.
 * 2. Redistributions in binary form must reproduce the above copyright
 *    notice, this list of conditions and the following disclaimer in the
 *    documentation and/or other materials provided with the distribution.
 *
 * THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS
 * OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
 * WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
 * ARE DISCLAIMED.  IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY
 * DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
 * DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS
 * OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
 * HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
 * LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY
 * OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF
 * SUCH DAMAGE.
 *
 */

/*
 * Trap handling subpage for R8000 systems.
 */

#include <machine/param.h>
#include <machine/asm.h>
#include <machine/cpu.h>
#include <mips64/mips_cpu.h>
#include <machine/pte.h>
#include <machine/regnum.h>
#include <machine/cpustate.h>

#include "assym.h"

#ifdef MIPS_PTE64
#error "R8000 doesn't need 64-bit PTE"
#endif

#define	TLBW	.align 4; .word 0x43000002

	.set	mips4
	.set	noreorder

	.text

	.align	12
	.globl	tfp_trapbase
tfp_trapbase:

	/*
	 * 000: tlb miss handler for U region
	 */
	.align	10

	.ent	utlb_miss, 0
utlb_miss:
	.set	noat
	.align	4
	PRE_MFC0_ADDR_HAZARD
	DMFC0	k0, COP_0_VADDR
	MFC0_HAZARD
	DMTC0	k0, COP_0_WORK0
	MTC0_HAZARD
	PTR_SRL	k0, SEGSHIFT
	sltiu	k1, k0, PMAP_SEGTABSIZE
	beqz	k1, _inv_seg
	 NOP
	DMFC0	k1, COP_0_UBASE		# PCB_SEGTAB(CI_CURPROCPADDR(curcpu))
	MFC0_HAZARD
	PTR_SLL	k0, LOGREGSZ
	PTR_ADDU k1, k0
	PTR_L	k1, 0(k1)		# get pointer to page directory
	DMFC0	k0, COP_0_WORK0		# saved COP_0_VADDR
	MFC0_HAZARD
	PTR_SRL	k0, (DIRSHIFT - LOGREGSZ)
	beqz	k1, _inv_seg
	 andi	k0, (NPDEPG - 1) << LOGREGSZ
	PTR_ADDU k1, k0
	PTR_L	k1, 0(k1)		# get pointer to page table
	DMFC0	k0, COP_0_WORK0		# saved COP_0_VADDR
	MFC0_HAZARD
	beqz	k1, _inv_seg
	 PTR_SRL k0, PAGE_SHIFT - PTE_LOG
	andi	k0, (NPTEPG - 1) << PTE_LOG
	PTR_ADDU k1, k0
	PTE_LOAD k0, 0(k1)		# get pte
	DMTC0	k0, COP_0_TLB_LO
	MTC0_HAZARD
	TLBW
	ERET

_inv_seg:
	DMFC0	k0, COP_0_STATUS_REG
	MFC0_HAZARD
	andi	k0, SR_KSU_USER
	bnez	k0, u_general
	 NOP
	b	k_general
	 NOP

	.set	at
	.end	utlb_miss

	/*
	 * 400: tlb miss handler for KV0 region
	 */
	.align	10

	.ent	kv0tlb_miss, 0
kv0tlb_miss:
	.set	noat
	/*
	 * Since we do not set up KV0 mappings, fall through directly
	 * into the `invalid address' fault handling.
	 */
	.align	4
	b	_inv_seg
	 NOP
	.set	at
	.end	kv0tlb_miss

	/*
	 * 800: tlb miss handler for KV1 region
	 */
	.align	10

	.ent	kv1tlb_miss, 0
kv1tlb_miss:
	.set	noat
	.align	4
	PRE_MFC0_ADDR_HAZARD
	DMFC0	k0, COP_0_VADDR
	MFC0_HAZARD
	DMFC0	k1, COP_0_STATUS_REG
	MFC0_HAZARD
	andi	k1, SR_KSU_USER
	bnez	k1, u_general
	 LA	k1, VM_MIN_KERNEL_ADDRESS
	PTR_SUBU k0, k1
	DMFC0	k1, COP_0_WORK1		# Sysmapsize
	MFC0_HAZARD
	PTR_SRL	k0, PAGE_SHIFT
	sltu	k1, k0, k1
	beqz	k1, k_general
	 PTR_SLL k0, 2
	DMFC0	k1, COP_0_GBASE		# Sysmap
	MFC0_HAZARD
	PTR_ADDU k1, k0
	PTE_LOAD k0, 0(k1)		# get pte
	DMTC0	k0, COP_0_TLB_LO
	MTC0_HAZARD
	TLBW
	ERET
	.set	at
	.end	kv1tlb_miss

	/*
	 * C00: tlb miss exception while servicing an exception
	 *	tlb invalid exception
	 *	tlb modified exception
	 *	all other exceptions
	 */
	.align	10
	.ent	exception, 0
exception:
	.set	noat
	.align	4
	DMFC0	k0, COP_0_STATUS_REG
	MFC0_HAZARD
	andi	k0, SR_KSU_USER
	DMFC0	k1, COP_0_CAUSE_REG
	MFC0_HAZARD
	bnez	k0, u_general
	 and	k1, CR_EXC_CODE

	LA	k0, k_exception_table
	PTR_ADDU k0, k1
	PTR_L	k1, 0(k0)
	j	k1
	 NOP

	.set	at
	.end	exception

	/*
	 * Handle a TLB invalid exception from kernel mode. This implies there
	 * is a TLB match, but currently not valid. Check if the pte is now
	 * valid and update, or pass the mess to the regular exception handler.
	 */
k_tlb_inv:
	.set	noat
	.align	4
	PRE_MFC0_ADDR_HAZARD
	DMFC0	k0, COP_0_VADDR
	MFC0_HAZARD
	LA	k1, VM_MIN_KERNEL_ADDRESS
	PTR_SUBU k0, k1
	DMFC0	k1, COP_0_WORK1		# Sysmapsize
	MFC0_HAZARD
	PTR_SRL	k0, PAGE_SHIFT
	sltu	k1, k0, k1
	beqz	k1, k_general
	 PTR_SLL k0, 2
	DMFC0	k1, COP_0_GBASE		# Sysmap
	MFC0_HAZARD
	PTR_ADDU k1, k0
	PTE_LOAD k0, 0(k1)		# get pte
	andi	k1, k0, PG_V
	beqz	k1, k_general		# if not valid
	 NOP

	DMTC0	k0, COP_0_TLB_LO
	MTC0_HAZARD
	TLBW
	ERET
	.set	at

k_exception_table:
	PTR_VAL	k_general	# interrupt
	PTR_VAL	k_general	# TLB modification
	PTR_VAL	k_tlb_inv
	PTR_VAL	k_tlb_inv
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general
	PTR_VAL	k_general

	.align	5
NNON_LEAF(u_general, FRAMESZ(CF_SZ), ra)
	.set	noat
	.mask	0x80000000, (CF_RA_OFFS - FRAMESZ(CF_SZ))

	GET_CPU_INFO(k1, k0)
	PTR_L	k0, CI_CURPROCPADDR(k1)
	SAVE_CPU(k0, 0)
	SAVE_CPU_SREG(k0, 0)
	PTR_ADDU sp, k0, USPACE-FRAMESZ(CF_SZ)
	LA	gp, _gp
	.set	at
	and	t0, a1, ~(SR_COP_1_BIT | SR_EXL | SR_INT_ENAB | SR_KSU_MASK)
	DMTC0	t0, COP_0_STATUS_REG
	MTC0_SR_IE_HAZARD

	jal	trap
	 PTR_S	a3, CF_RA_OFFS(sp)		# for debugging

	DMFC0	t0, COP_0_STATUS_REG	# enable interrupts before checking
	ori	t0, SR_INT_ENAB		# for ast.
	DMTC0	t0, COP_0_STATUS_REG
	MTC0_SR_IE_HAZARD

0:
	GET_CPU_INFO(t1, t0)
	PTR_L	t0, CI_CURPROC(t1)
	lw	v0, P_ASTPENDING(t0)	# any pending AST?
	beq	v0, zero, 4f
	 NOP

	jal	ast
	 NOP

	b	0b
	 NOP

4:
	DMFC0	t0, COP_0_STATUS_REG	# disable interrupts
	MFC0_HAZARD
	ori	t0, SR_INT_ENAB
	xori	t0, SR_INT_ENAB
	DMTC0	t0, COP_0_STATUS_REG
	MTC0_SR_IE_HAZARD

	ori	t0, SR_EXL		# restoring to user mode.
	DMTC0	t0, COP_0_STATUS_REG	# must set exception level bit.
	MTC0_SR_IE_HAZARD

	# t1 is curcpu() from earlier
	move	k1, t1
	PTR_L	k0, CI_CURPROCPADDR(k1)
	RESTORE_REG(a3, CPL, k0, 0)
	sw	a3, CI_IPL(k1)
	.set	noat
	RESTORE_CPU_SREG(k0, 0)
	RESTORE_REG(a0, PC, k0, 0)
	RESTORE_CPU(k0, 0)
	RESTORE_REG(sp, SP, k0, 0)
	LI	k0, 0
	LI	k1, 0
	ERET
	.set	at
END(u_general)

	.align	5
NNON_LEAF(k_general, FRAMESZ(KERN_EXC_FRAME_SIZE), ra)
	.globl	k_intr				# for trap.c peace of mind
k_intr:
	.set	noat
	.mask	0x80000000, (CF_RA_OFFS - FRAMESZ(KERN_EXC_FRAME_SIZE))

#ifdef DEBUG
	GET_CPU_INFO(k1, k0)
	PTR_L	k1, CI_CURPROCPADDR(k1)
	PTR_SUBU k0, sp, k1
	sltiu	k0, 2048
	beqz	k0, 8f
	 NOP

	LA	a0, start - FRAMESZ(CF_SZ) - 4 * REGSZ
	move	a6, sp
	move	sp, a0
	DMFC0	a1, COP_0_EXC_PC
	MFC0_HAZARD
	LA	a0, 1f
	PRE_MFC0_ADDR_HAZARD
	DMFC0	a3, COP_0_VADDR
	MFC0_HAZARD
	DMFC0	a4, COP_0_STATUS_REG
	MFC0_HAZARD
	DMFC0	a5, COP_0_CAUSE_REG
	MFC0_HAZARD
	jal	printf
	 move	a2, ra

	LA	sp, start-FRAMESZ(CF_SZ)
#ifdef DDB
	LA	a1, db_printf
	LA	a0, 2f
	jal	trapDump
	 NOP
#endif
	PANIC("kernel stack overflow")

	.data
1:
	.asciiz "\rtfptrap: PC %p RA %p ADR %p\nSR %p CR %p SP %p\n"
2:
	.asciiz	"stack oflow"

	.text
8:
#endif

	PTR_SUB	k0, sp, FRAMESZ(KERN_EXC_FRAME_SIZE)
	SAVE_CPU(k0, CF_RA_OFFS)
#if defined(DDB)
	SAVE_CPU_SREG(k0, CF_RA_OFFS)
#endif
	.set	at
	move	sp, k0			# Already on kernel stack
	LA	gp, _gp
	and	t0, a1, ~(SR_COP_1_BIT | SR_EXL | SR_INT_ENAB | SR_KSU_MASK)
	DMTC0	t0, COP_0_STATUS_REG
	MTC0_SR_IE_HAZARD
	PTR_S	a0, 0(sp)
	jal	trap
	 PTR_S	a3, CF_RA_OFFS + KERN_REG_SIZE(sp)

	DMFC0	t0, COP_0_STATUS_REG	# disable interrupts
	MFC0_HAZARD
	ori	t0, SR_INT_ENAB
	xori	t0, SR_INT_ENAB
	DMTC0	t0, COP_0_STATUS_REG
	MTC0_SR_IE_HAZARD

	.set	noat
	RESTORE_REG(a0, PC, sp, CF_RA_OFFS)
	RESTORE_CPU(sp, CF_RA_OFFS)
	PTR_ADDU sp, sp, FRAMESZ(KERN_EXC_FRAME_SIZE)
	ERET
	.set	at
END(k_general)
@


1.5
log
@Define PTE_* macros in one place. Use the lwu instruction for 32-bit PTE
loads on all systems as the TLB code does not need sign extension.
@
text
@d1 1
a1 1
/*	$OpenBSD: exception_tfp.S,v 1.4 2015/09/20 11:52:33 miod Exp $	*/
d98 7
@


1.4
log
@Correctly compute the userland pte index in a pte page in the userland tlb miss
handler; from Naruaki Etomi (nullnilaki on gmail), thanks!
@
text
@d1 1
a1 1
/*	$OpenBSD: exception_tfp.S,v 1.3 2014/04/09 21:10:35 miod Exp $	*/
a63 3
#else
#define	PTE_LOG		2
#define	PTE_LOAD	lwu
@


1.3
log
@Make trapDump() take the printf-like function as a 2nd parameter.
No functional change; helps non-released kernels with extra debugging code (-:
@
text
@d1 1
a1 1
/*	$OpenBSD: exception_tfp.S,v 1.2 2012/10/03 11:18:23 miod Exp $	*/
d62 7
d106 2
a107 2
	 PTR_SRL k0, PAGE_SHIFT - 2
	andi	k0, ((NPTEPG / 2) - 1) << 2
d109 1
a109 1
	lwu	k0, 0(k1)		# get pte
d172 1
a172 1
	lwu	k0, 0(k1)		# get pte
d230 1
a230 1
	lwu	k0, 0(k1)		# get pte
@


1.2
log
@Split ever-growing mips <machine/cpu.h> into what 99% of the kernel needs,
which will remain in <machine/cpu.h>, and a new mips_cpu.h containing only the
goriest md details, which are only of interest to a handful set of files; this
is similar in spirit to what alpha does, but here <machine/cpu.h> does not
include the new file.
@
text
@d1 1
a1 1
/*	$OpenBSD: exception_tfp.S,v 1.1 2012/09/29 21:37:03 miod Exp $	*/
d366 1
@


1.1
log
@Basic R8000 processor support. R8000 processors require MMU-specific code,
exception-specific code, clock-specific code, and L1 cache-specific code. L2
cache is per-design, of which only two exist: SGI Power Indigo2 (IP26) and SGI
Power Challenge (IP21) and are not covered by this commit.

R8000 processors also are 64-bit only processors with 64-bit coprocessor 0
registers, and lack so-called ``compatibility'' memory spaces allowing 32-bit
code to run with sign-extended addresses and registers.

The intrusive changes are covered by #ifdef CPU_R8000 stanzas. However,
trap() is split into a high-level wrapper and a new function, itsa(),
responsible for the actual trap servicing (which name couldn't be helped
because I'm an incorrigible punster). While an R8000 exception may cause
(via trap() ) multiple exceptions to be serviced, non-R8000 processors will
always service one exception in trap(), but they are nevertheless affected
by this code split.
@
text
@d1 1
a1 1
/*	$OpenBSD$	*/
d55 1
@

